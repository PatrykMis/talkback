// For building the open-source release of TalkBack.

apply plugin: 'com.android.application'

apply from: 'shared.gradle'

dependencies {
    implementation project(':talkback')
}

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.9.7'
        classpath 'com.android.tools.build:gradle:7.0.4'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

android {
    buildToolsVersion '31.0.0'
    defaultConfig {
        applicationId talkbackApplicationId
        versionName "370044210"
        versionCode 370044210
        manifestPlaceholders = [talkbackMainPermission:talkbackMainPermission]
        minSdkVersion 26
        targetSdkVersion 31
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        multiDexEnabled true
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'README'
        exclude 'TODO'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

task createTranslationTablesZip(type: Zip){
    archiveFileName="translationtables.zip"
    destinationDirectory = file('braille/src/main/res/raw/')
    from fileTree('./braille/src/main/tables/')
    into('liblouis/tables')
}

preBuild.dependsOn createTranslationTablesZip
