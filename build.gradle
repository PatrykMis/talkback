// For building the open-source release of TalkBack.

apply plugin: 'com.android.application'

apply from: 'shared.gradle'

dependencies {
    implementation project(':talkback')
}

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.9.9.1'
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

android {
    compileSdk 33
    buildToolsVersion '33.0.0'
    namespace talkbackApplicationId
    defaultConfig {
        applicationId talkbackApplicationId
        versionName "12.2"
        versionCode 1
        manifestPlaceholders = [talkbackMainPermission:talkbackMainPermission]
        minSdk 26
        targetSdk 32
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'README'
        exclude 'DebugProbesKt.bin'
        exclude 'META-INF/**.version'
        exclude 'kotlin-tooling-metadata.json'
        exclude 'kotlin/**.kotlin_builtins'
    }
    lint {
        checkReleaseBuilds false
        abortOnError false
    }
    productFlavors {
        phone {
            dimension "target"
        }
        wear {
            dimension "target"
            versionNameSuffix "-wear"
        }
    }
}
